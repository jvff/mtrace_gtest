apply plugin: 'cpp'

model {
    repositories {
        lib(PrebuiltLibraries) {
            gtest {
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("/usr/lib/x86_64-linux-gnu/libgtest.a")
                }
            }
            gtest_main {
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("/usr/lib/x86_64-linux-gnu/libgtest_main.a")
                }
            }
            fakeit {
                headers.srcDir "/home/janito/lib/fakeit/include"
                headers.srcDir "/home/janito/lib/fakeit/config/gtest"
            }
        }
    }

    components {
        mtrace_gtest(NativeLibrarySpec) {
            sources.cpp.exportedHeaders {
                srcDir 'include/mtrace_gtest/cpp'
                include '**/*.hpp'
            }
        }
        mtrace_gtest_main(NativeLibrarySpec) {
            sources.cpp {
                lib library: 'mtrace_gtest'
            }
        }
        test(NativeExecutableSpec) {
            binaries.all {
                cppCompiler.args "-std=c++11"
                linker.args "-pthread"
            }
            sources.cpp {
                lib library: 'mtrace_gtest', linkage: 'static'
                lib library: 'gtest', linkage: 'static'
                lib library: 'fakeit', linkage: 'api'
                lib library: 'gtest_main', linkage: 'static'
            }
        }
        sample(NativeExecutableSpec) {
            binaries.all {
                linker.args "-pthread"
            }
            sources.cpp {
                lib library: 'mtrace_gtest_main', linkage: 'static'
                lib library: 'mtrace_gtest', linkage: 'static'
                lib library: 'gtest', linkage: 'static'
            }
        }
    }
}

task checkHeaderGuards(type: com.janitovff.headerguardplugin.HeaderGuardTask)

task test(type: Exec, dependsOn: ['testExecutable', 'checkHeaderGuards']) {
    doFirst {
        def binary = binaries.getByName 'testExecutable'
        def executableFile = binary.getExecutableFile()

        commandLine executableFile.toString(), '--gtest_color=yes'
    }
}
